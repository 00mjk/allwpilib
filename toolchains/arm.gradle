def compilerPrefix = 'arm-frc-linux-gnueabi-'
model {
    toolChains {
        gcc(Gcc) {
            target("arm"){
                // We use a custom-built cross compiler with the prefix arm-frc-linux-gnueabi-<util name>
                // If this ever changes, the prefix will need to be changed here
                cppCompiler.executable = compilerPrefix + cppCompiler.executable
                linker.executable = compilerPrefix + linker.executable
                assembler.executable = compilerPrefix + assembler.executable
                // Gradle auto-adds the -m32 argument to the linker and compiler. Our compiler only supports
                // arm, and doesn't understand this flag, so it is removed from both
                cppCompiler.withArguments { args ->
                    args << '-std=c++1y' << '-Wformat=2' << '-Wall' << '-Wextra' << '-Werror' << '-pedantic'
                    args << '-Wno-psabi' << '-Wno-unused-parameter' << '-fPIC' << '-rdynamic'
                    //TODO: When the compiler allows us to actually call deprecated functions from within
                    // deprecated function, remove this line (this will cause calling deprecated functions
                    // to be treated as a warning rather than an error).
                    args << '-Wno-error=deprecated-declarations'
                    args.remove('-m32')
                }
                linker.withArguments { args ->
                    args << '-rdynamic'
                    args.remove('-m32')
                }
                staticLibArchiver.executable = compilerPrefix + staticLibArchiver.executable
            }
        }
    }
}

ext.setupReleaseDefines = { cppCompiler, linker ->
    cppCompiler.args '-O2', '-g'
}

ext.setupDebugDefines = { cppCompiler, linker ->
    cppCompiler.args '-g', '-O0'
}

ext.releaseSetup = { releaseTask ->
    binaries.withType(SharedLibraryBinarySpec) { binary ->
        if (!project.hasProperty('debug')) {
            def library = binary.sharedLibraryFile.absolutePath
            def debugLibrary = binary.sharedLibraryFile.absolutePath + ".debug"
            if (project.tasks.findByName("firstObjcopy${binary.name}") == null) {
                def firstObjcopy = project.tasks.create("firstObjcopy${binary.name}", Exec) { task ->
                    task.commandLine "${compilerPrefix}objcopy", '--only-keep-debug', library, debugLibrary
                }
                def strip = project.tasks.create("strip${binary.name}", Exec) { task ->
                    task.commandLine "${compilerPrefix}strip", '-g', library
                }
                def secondObjcopy = project.tasks.create("secondObjcopy${binary.name}", Exec) { task ->
                    task.commandLine "${compilerPrefix}objcopy", "--add-gnu-debuglink=$debugLibrary", library
                }
                secondObjcopy.dependsOn strip
                strip.dependsOn firstObjcopy
                binary.tasks.whenObjectAdded { task ->
                    if (task.name.contains('link')) {
                        firstObjcopy.dependsOn task
                    }
                }
            }
            releaseTask.dependsOn project.tasks.getByName("secondObjcopy${binary.name}")
        }
    }
}
